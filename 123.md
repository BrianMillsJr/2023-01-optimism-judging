NotFound

medium

# User loss of funds on L2StandardBridge when withdrawing tokens and ETH

## Summary

In the `L2StandardBridge` contract is possible to call `withdraw` and `withdrawTo` functions sending both ETH and a valid L2 Token, losing all user ETH sent to the contract.

## Vulnerability Detail

The user thinks he can send both ETH and a valid token to the `L2StandardBridge` in order to  withdraw them to L1, he finds a way to send both together, by calling `withdraw` or `withdrawTo` function, sending some amount of ETH, and setting a valid l2Token.

The function will call `_initiateWithdrawal`:

```solidity
function _initiateWithdrawal(
  address _l2Token,
  address _from,
  address _to,
  uint256 _amount,
  uint32 _minGasLimit,
  bytes calldata _extraData
) internal {
	address l1Token = OptimismMintableERC20(_l2Token).l1Token();
	if (_l2Token == Predeploys.LEGACY_ERC20_ETH) {
	  _initiateBridgeETH(_from, _to, _amount, _minGasLimit, _extraData);
	} else {
	  _initiateBridgeERC20(_l2Token, l1Token, _from, _to, _amount, _minGasLimit, _extraData);
	}
	emit WithdrawalInitiated(l1Token, _l2Token, _from, _to, _amount, _extraData);
}
```

Considering `_l2Token` as valid, the function `_initiateBridgeERC20` will execute the `else` clause; not checking the `msg.value` sent to the bridge by the user and only bridging the `_l2Token` to L1. Making the user lose his ETH funds sent to the bridge.

### PoC

Proof of concept tests written in Foundry

```solidity
// Optimism L2 Standard Bridge
IL2StandardBridge l2StandardBridge = IL2StandardBridge(0x4200000000000000000000000000000000000010);
// Optimism Useless Token
IERC20 OUTb = IERC20(0x3e7eF8f50246f725885102E8238CBba33F276747);
//user
address alice = address(1);

function setUp() public {
    optimismFork = vm.createFork(OPTIMISM_RPC_URL);
    vm.selectFork(optimismFork);
    deal(address(OUTb), alice, 10 ether);
    startHoax(alice, 10 ether);
}

function testLossOfFundsWithdraw() public {
    OUTb.approve(address(l2StandardBridge), 10 ether);
    // user tries to withdraw 10 ether & 10 tokens
    l2StandardBridge.withdraw{value: 10 ether }(address(OUTb), 10 ether, 5000, "");

    //10 tokens gets transfered to the bridge and sent to L1
    assertEq(OUTb.balanceOf(alice), 0);
    //user loses 10 ether that gets locked into the L2StandardBridge
    assertEq(alice.balance, 0);
}

function testLossOfFundsWithdrawTo() public {
    OUTb.approve(address(l2StandardBridge), 10 ether);
    // user tries to withdraw 10 ether & 10 tokens
    l2StandardBridge.withdrawTo{value: 10 ether }(address(OUTb), alice, 10 ether, 5000, "");

    //10 tokens gets transfered to the bridge and sent to L1
    assertEq(OUTb.balanceOf(alice), 0);
    //user loses 10 ether that gets locked into the L2StandardBridge
    assertEq(alice.balance, 0);
}
```

## Impact

Loss of user funds due to lack o validation

## Code Snippet

https://github.com/sherlock-audit/2023-01-optimism/blob/5538f31520825f79071536c7f11be149b83ccef0/optimism/packages/contracts-bedrock/contracts/L2/L2StandardBridge.sol#L80-L87

```solidity
function withdraw(
  address _l2Token,
  uint256 _amount,
  uint32 _minGasLimit,
  bytes calldata _extraData
) external payable virtual onlyEOA {
  _initiateWithdrawal(_l2Token, msg.sender, msg.sender, _amount, _minGasLimit, _extraData);
}
```

https://github.com/sherlock-audit/2023-01-optimism/blob/5538f31520825f79071536c7f11be149b83ccef0/optimism/packages/contracts-bedrock/contracts/L2/L2StandardBridge.sol#L103-L111

```solidity
function withdrawTo(
  address _l2Token,
  address _to,
  uint256 _amount,
  uint32 _minGasLimit,
  bytes calldata _extraData
) external payable virtual {
  _initiateWithdrawal(_l2Token, msg.sender, _to, _amount, _minGasLimit, _extraData);
}
```

## Tool used

Manual Review, Foundry, Forge

## Recommendation

It is recommended to verify if `msg.value` is different from 0 in `_initiateWithdrawal` for the token path:

```diff
function _initiateWithdrawal(
  address _l2Token,
  address _from,
  address _to,
  uint256 _amount,
  uint32 _minGasLimit,
  bytes calldata _extraData
) internal {
	address l1Token = OptimismMintableERC20(_l2Token).l1Token();
	if (_l2Token == Predeploys.LEGACY_ERC20_ETH) {
	  _initiateBridgeETH(_from, _to, _amount, _minGasLimit, _extraData);
	} else {
+		require(msg.value == 0, "Impossible to make withdraw");
	  _initiateBridgeERC20(_l2Token, l1Token, _from, _to, _amount, _minGasLimit, _extraData);
	}
	emit WithdrawalInitiated(l1Token, _l2Token, _from, _to, _amount, _extraData);
}
```